import gg.jte.ContentType

plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'gg.jte.gradle' version '3.2.1'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

ext {
    springBootVersion = '3.5.9'
    junitVersion = '5.11.0'
    assertjVersion = '3.26.3'
    lombokVersion = '1.18.38'
    mockitoVersion = '5.21.0'
    servletVersion = '6.1.0'
    tomcatVersion = '11.0.12'
    jteVersion = '3.2.1'
    jteSpringBootVersion = '3.2.1'
    validationApiVersion = '4.0.0-M1'
    hibernateValidatorVersion = '9.1.0.Final'

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'ru.mentee.power.crm.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"

    implementation "gg.jte:jte:${jteVersion}"
    implementation "gg.jte:jte-runtime:${jteVersion}"

    // Spring Boot Starters
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation 'org.springframework.retry:spring-retry'

    // JTE интеграция для Spring Boot
    implementation "gg.jte:jte-spring-boot-starter-3:${jteSpringBootVersion}"
    implementation "gg.jte:jte:${jteVersion}"

    // Валидация
    implementation "jakarta.validation:jakarta.validation-api:${validationApiVersion}"
    implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
    implementation("org.springframework.boot:spring-boot-starter-validation:4.0.1")

    // @RecordBuilder
    annotationProcessor 'io.soabase.record-builder:record-builder-processor:51'
    compileOnly 'io.soabase.record-builder:record-builder-core:51'

    // PostgreSQL JDBC Driver (для dev и production)
    runtimeOnly 'org.postgresql:postgresql'

    // H2 Database (только для тестов)
    testRuntimeOnly 'com.h2database:h2:2.3.232'

    // Для тестирования
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation 'net.bytebuddy:byte-buddy:1.17.7'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.17.7'
}

bootRun {
    mainClass = 'ru.mentee.power.crm.Application'
    args = ['--server.port=8081']
}

bootJar {
    archiveClassifier = 'spring-boot'
    exclude 'ru/mentee/power/crm/Main.class'
}

jar {
    archiveClassifier = ''
    manifest {
        attributes 'Main-Class': 'ru.mentee.power.crm.Main'
    }
    exclude 'ru/mentee/power/crm/Application.class'
}

checkstyle {
    toolVersion = '10.12.4'
    maxErrors = 0
    maxWarnings = 999
}

jacoco {
    toolVersion = "0.8.14"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jvmArgs = ['-XX:+EnableDynamicAgentLoading']

    doFirst {
        def mockitoAgent = configurations.testRuntimeClasspath
                .resolve()
                .find { it.name.startsWith('mockito-core') }
        if (mockitoAgent) {
            jvmArgs += "-javaagent:${mockitoAgent}"
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'ru/mentee/power/crm/Main.class',
                    'ru/mentee/power/crm/web/**/HelloCrmServer*.class',
                    'ru/mentee/power/crm/util/**/TestDataUtils*.class',
                    'ru/mentee/power/crm/config/**/TestDataInitializer*.class',
                    'ru/mentee/power/crm/model/LeadBuilder*.class',
                    'ru/mentee/power/crm/domain/Deal*.class',
                    'ru/mentee/power/crm/spring/controller/DealController*.class',
                    'ru/mentee/power/crm/spring/exception/LeadNotFoundException*.class',
                    'ru/mentee/power/crm/spring/exception/IllegalLeadStateException*.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'ru/mentee/power/crm/Main.class',
                    'ru/mentee/power/crm/web/**/HelloCrmServer*.class',
                    'ru/mentee/power/crm/util/**/TestDataUtils*.class',
                    'ru/mentee/power/crm/config/**/TestDataInitializer*.class',
                    'ru/mentee/power/crm/model/LeadBuilder*.class',
                    'ru/mentee/power/crm/domain/Deal*.class',
                    'ru/mentee/power/crm/spring/controller/DealController*.class',
                    'ru/mentee/power/crm/spring/exception/LeadNotFoundException*.class',
                    'ru/mentee/power/crm/spring/exception/IllegalLeadStateException*.class'
            ])
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

run {
    standardInput = System.in
    systemProperty "file.encoding", "UTF-8"
}

jte {
    sourceDirectory = file("src/main/jte").toPath()
    contentType = ContentType.Html
    binaryStaticContent = false
}