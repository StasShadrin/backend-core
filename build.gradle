import gg.jte.ContentType

plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'gg.jte.gradle' version '3.2.1'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '8.2.1'
    id 'org.openapi.generator' version '7.1.0'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

ext {
    springBootVersion = '3.5.8'
    junitVersion = '5.11.0'
    assertjVersion = '3.27.7'
    lombokVersion = '1.18.38'
    mapstructVersion = '1.6.3'
    lombokMapstructBindingVersion = '0.2.0'
    mockitoVersion = '5.21.0'
    servletVersion = '6.1.0'
    tomcatVersion = '11.0.12'
    jteVersion = '3.2.1'
    jteSpringBootVersion = '3.2.1'
    validationApiVersion = '4.0.0-M1'
    hibernateValidatorVersion = '9.1.0.Final'
    liquibaseVersion = '5.0.1'
    spotlessVersion = '8.2.1'
    openApiGeneratorVersion = '7.1.0'
    springdocVersion = '2.8.5'
}

spotless {
    java {
        target 'src/**/*.java'
        targetExclude 'build/**'
        googleJavaFormat('1.34.1')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'ru.mentee.power.crm.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    // Lombok (должен быть первым в annotationProcessor)
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // lombok-mapstruct-binding (критически важно!)
    implementation "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    // MapStruct (должен быть после Lombok в annotationProcessor)
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"

    implementation "gg.jte:jte-runtime:${jteVersion}"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"

    // Spring Boot Starters
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"

    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Resilience4j
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'
    implementation 'io.github.resilience4j:resilience4j-feign:2.2.0'

    // JTE
    implementation "gg.jte:jte-spring-boot-starter-3:${jteSpringBootVersion}"
    implementation "gg.jte:jte:${jteVersion}"

    // RecordBuilder
    annotationProcessor 'io.soabase.record-builder:record-builder-processor:51'
    compileOnly 'io.soabase.record-builder:record-builder-core:51'

    // Liquibase
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // H2 for tests
    testRuntimeOnly 'com.h2database:h2:2.3.232'

    // Test dependencies
    testImplementation 'org.wiremock:wiremock-standalone:3.4.2'
    testImplementation 'org.instancio:instancio-junit:5.5.1'
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

bootRun {
    mainClass = 'ru.mentee.power.crm.Application'
    args = ['--server.port=8081']
}

bootJar {
    archiveClassifier = 'spring-boot'
    exclude 'ru/mentee/power/crm/Main.class'
}

jar {
    archiveClassifier = ''
    manifest {
        attributes 'Main-Class': 'ru.mentee.power.crm.Main'
    }
    exclude 'ru/mentee/power/crm/Application.class'
}

checkstyle {
    toolVersion = '10.12.4'
    maxErrors = 0
    maxWarnings = 999
    configProperties = [
            'checkstyle.cache.file': "${buildDir}/checkstyle/cachefile"
    ]
}

tasks.withType(Checkstyle).configureEach {
    exclude '**/generated/**'
    exclude '**/build/**'

    reports {
        html.required = true
        xml.required = false
    }
}

checkstyleMain {
    source = sourceSets.main.allJava.filter { file ->
        !file.path.contains('generated') &&
                !file.path.contains('build/')
    }
}

checkstyleTest {
    source = sourceSets.test.allJava
}

jacoco {
    toolVersion = "0.8.14"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    jvmArgs = ['-XX:+EnableDynamicAgentLoading']

    doFirst {
        def mockitoAgent = configurations.testRuntimeClasspath
                .resolve()
                .find { it.name.startsWith('mockito-core') }
        if (mockitoAgent) {
            jvmArgs += "-javaagent:${mockitoAgent}"
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/generated/**/*.class',
                    'ru/mentee/power/crm/Main.class',
                    'ru/mentee/power/crm/Application.class',
                    'ru/mentee/power/crm/spring/TestBadFormat.class',
                    'ru/mentee/power/crm/web/**/HelloCrmServer*.class',
                    'ru/mentee/power/crm/util/**/TestDataUtils*.class',
                    'ru/mentee/power/crm/config/**/TestDataInitializer*.class',
                    'ru/mentee/power/crm/model/LeadBuilder*.class',
                    'ru/mentee/power/crm/domain/**/*.class',
                    'ru/mentee/power/crm/spring/controller/DealController*.class',
                    'ru/mentee/power/crm/spring/service/LeadLockingService*.class',
                    'ru/mentee/power/crm/spring/exception/LeadNotFoundException*.class',
                    'ru/mentee/power/crm/spring/exception/IllegalLeadStateException*.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/generated/**/*.class',
                    'ru/mentee/power/crm/Main.class',
                    'ru/mentee/power/crm/Application.class',
                    'ru/mentee/power/crm/spring/TestBadFormat.class',
                    'ru/mentee/power/crm/web/**/HelloCrmServer*.class',
                    'ru/mentee/power/crm/util/**/TestDataUtils*.class',
                    'ru/mentee/power/crm/config/**/TestDataInitializer*.class',
                    'ru/mentee/power/crm/model/LeadBuilder*.class',
                    'ru/mentee/power/crm/domain/**/*.class',
                    'ru/mentee/power/crm/spring/controller/DealController*.class',
                    'ru/mentee/power/crm/spring/service/LeadLockingService*.class',
                    'ru/mentee/power/crm/spring/exception/LeadNotFoundException*.class',
                    'ru/mentee/power/crm/spring/exception/IllegalLeadStateException*.class'
            ])
        }))
    }

    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

run {
    standardInput = System.in
    systemProperty "file.encoding", "UTF-8"
}

jte {
    sourceDirectory = file("src/main/jte").toPath()
    contentType = ContentType.Html
    binaryStaticContent = false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.1"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=ERROR'
    ]
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "C:/Projects/backend-core/src/main/resources/api/api-spec.yaml"
    outputDir = "${buildDir}/generated"
    apiPackage = "ru.mentee.power.crm.spring.rest.generated"
    modelPackage = "ru.mentee.power.crm.spring.dto.generated"
    configOptions = [
            interfaceOnly: "true",
            useTags: "true",
            dateLibrary: "java8",
            useSpringBoot3: "true",
            documentationProvider: "none",
            useRecords: "true",
            hideGenerationTimestamp: "true",
            openApiNullable: "false"
    ]
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate